/*
 * MIT License
 *
 * Copyright (c) 2018 objecttrouve.org <un.object.trouve@gmail.com>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 */
import net.researchgate.release.GitAdapter

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    }
}

subprojects {


    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'net.researchgate.release'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.hierynomus.license'

    group='org.objecttrouve'

    sourceCompatibility=JavaVersion.VERSION_1_8
    targetCompatibility=JavaVersion.VERSION_1_8

    configurations {
        licenses {
            dependencies.addAll(configurations.implementation.dependencies)
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar, javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }


    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    //noinspection GroovyAssignabilityCheck
                    name '42 Graphs'
                    packaging 'jar'
                    description 'Convenient Hamcrest Matcher derivations'
                    url 'https://github.com/objecttrouve/42graphs'

                    scm {
                        url 'https://github.com/objecttrouve/42graphs'
                        connection 'scm:https://github.com/objecttrouve/42graphs.git'
                        developerConnection 'scm:git://github.com/objecttrouve/42graphs.git'
                    }

                    licenses {
                        license {
                            //noinspection GroovyAssignabilityCheck
                            name 'MIT License'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'objecttrouve'
                            //noinspection GroovyAssignabilityCheck
                            name "L'objet trouv√©"
                            email 'un.object.trouve@gmail.com'
                        }
                    }
                }
            }
        }
    }

    release {
        scmAdapters = [
                GitAdapter
        ]
    }

    afterReleaseBuild.dependsOn uploadArchives

    downloadLicenses {
        ext.mit= license('MIT License', 'https://opensource.org/licenses/MIT')

        includeProjectDependencies = true
        //noinspection GroovyGStringKey
        licenses = [
                (group('org.objecttrouve')) : license('MIT'),
        ]

        aliases = [
                (mit) : license('MIT License', 'https://opensource.org/licenses/MIT')
        ]

        dependencyConfiguration = 'licenses'
    }

}

